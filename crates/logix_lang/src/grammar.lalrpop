use std::str::FromStr;
use std::collections::HashMap;
use crate::ast::prelude::*;

grammar;

pub ID: String = {
    <s:r"[a-zA-Z_][a-zA-Z0-9_]*"> => s.to_string(),
};

pub Path: String = {
    <f:ID> => f,
    <f:ID> "." <p:Path> => format!("{}/{}", f, p),
};

Num: usize = <s:r"[0-9]+"> => s.parse::<usize>().unwrap();
Float: f64 = <s:r"[0-9]+\.[0-9]+"> => f64::from_str(s).unwrap();

pub PinAddr: PinAddr = {
    <comp:ID> => PinAddr::ByIdx(comp, 0),
    <comp:ID> "." <pin:Num> => PinAddr::ByIdx(comp, pin),
    <comp:ID> "." <pin:ID> => PinAddr::ByName(comp, pin),
};

pub Conn: ConnDecl = {
    <src:PinAddr> "->" <dest:PinAddr> => ConnDecl { src, dest },
};

pub ConnItems: Vec<ConnDecl> = {
    <c:Conn> => vec![c],
    <c:Conn> "," <cs:ConnItems> => {
        vec![c].into_iter().chain(cs.into_iter()).collect()
    },
}

pub CompInst: Comp = {
    <name:ID> => Comp::from_name(&name, 0, 0.0),
    <name:ID> "(" <n:Num> ")" => Comp::from_name(&name, n, 0.0),
    <name:ID> "(" <f:Float> ")" => Comp::from_name(&name, 0, f),
}

pub SubcItems: Vec<(String, Comp)> = {
    <name:ID> "=" <comp:CompInst> => vec![(name, comp)],
    <name:ID> "=" <comp:CompInst> "," <cs:SubcItems> => {
        cs.into_iter().chain(vec![(name, comp)].into_iter()).collect()
    },
}

pub SubcDecl: HashMap<(String, usize), Comp> = {
    "subc" "(" <cs:SubcItems> ")" => {
        let mut map = HashMap::new();
        let mut i: usize = cs.len();
        for (name, comp) in cs {
            i -= 1;
            map.insert((name, i), comp);
        }
        map
    },
}

pub DesignDecl: Vec<ConnDecl> = {
    "design" "(" <cs:ConnItems> ")" => cs,
}

pub IDItems: Vec<String> = {
    <name:ID> => vec![name],
    <name:ID> "," <ns:IDItems> => {
        vec![name].into_iter().chain(ns.into_iter()).collect()
    },
}

pub CompDecl: CompDecl = {
    <name:ID> "(" <subc:SubcDecl> <design:DesignDecl> ")" => {
        CompDecl::new(name, subc, design)
    }
}

pub CompDeclItems: Vec<CompDecl> = {
    <c:CompDecl> => vec![c],
    <c:CompDecl> <cs:CompDeclItems> => {
        vec![c].into_iter().chain(cs.into_iter()).collect()
    },
}

pub ImportStatm: String = {
    "use" <path:Path> ";" => path,
}

pub ImportItems: Vec<String> = {
    <u:ImportStatm> => vec![u],
    <u:ImportStatm> <us:ImportItems> => {
        vec![u].into_iter().chain(us.into_iter()).collect()
    },
}

pub Circuit: Circuit = {
    <imports: ImportItems?> <c:CompDeclItems> => Circuit { imports, comps: c },
};
