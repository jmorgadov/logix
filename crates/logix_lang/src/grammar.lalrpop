use std::str::FromStr;
use std::collections::HashMap;
use crate::ast::prelude::*;

grammar;

pub ID: String = {
    <s:r"[a-zA-Z_][a-zA-Z0-9_]*"> => s.to_string(),
};

pub Path: String = {
    <f:ID> => f,
    <f:ID> "." <p:Path> => format!("{}/{}", f, p),
};

Num: usize = <s:r"[0-9]+"> => s.parse::<usize>().unwrap();
Float: f64 = <s:r"[0-9]+\.[0-9]+"> => f64::from_str(s).unwrap();

pub PinAddr: PinAddr = {
    <comp:ID> => PinAddr::External(comp, PinIndexing::NoIndex),
    <comp:ID> "." <pin:Num> => PinAddr::InternalIdx(comp, pin, PinIndexing::NoIndex),
    <comp:ID> "." <pin:ID> => PinAddr::InternalName(comp, pin, PinIndexing::NoIndex),

    <comp:ID> "[" <idx:Num> "]" => PinAddr::External(comp, PinIndexing::Index(idx as u8)),
    <comp:ID> "." <pin:Num> "[" <idx:Num> "]"=> PinAddr::InternalIdx(comp, pin, PinIndexing::Index(idx as u8)),
    <comp:ID> "." <pin:ID> "[" <idx:Num> "]" => PinAddr::InternalName(comp, pin, PinIndexing::Index(idx as u8)),

    <comp:ID> "[" <idx_i:Num> ".." <idx_j:Num> "]" => PinAddr::External(comp, PinIndexing::Range(idx_i as u8, idx_j as u8)),
    <comp:ID> "." <pin:Num> "[" <idx_i:Num> ".." <idx_j:Num> "]"=> PinAddr::InternalIdx(comp, pin, PinIndexing::Range(idx_i as u8, idx_j as u8)),
    <comp:ID> "." <pin:ID> "[" <idx_i:Num> ".." <idx_j:Num> "]"=> PinAddr::InternalName(comp, pin, PinIndexing::Range(idx_i as u8, idx_j as u8)),
};

pub PinDecl: PinDecl = {
    <name:ID> => PinDecl { name, len: 1},
    <name:ID> ":" <len:Num> => PinDecl { name, len: len as u8 },
};

pub PinDeclItems: Vec<PinDecl> = {
    <p:PinDecl> => vec![p],
    <p:PinDecl> "," <ps:PinDeclItems> => {
        vec![p].into_iter().chain(ps.into_iter()).collect()
    },
};

pub Conn: ConnDecl = {
    <src:PinAddr> "->" <dest:PinAddr> => ConnDecl { src, dest },
};

pub ConnItems: Vec<ConnDecl> = {
    <c:Conn> => vec![c],
    <c:Conn> "," <cs:ConnItems> => {
        vec![c].into_iter().chain(cs.into_iter()).collect()
    },
}

pub CompInst: Comp = {
    <name:ID> => Comp::from_name(&name, 0, 0.0),
    <name:ID> "(" <n:Num> ")" => Comp::from_name(&name, n, 0.0),
    <name:ID> "(" <f:Float> ")" => Comp::from_name(&name, 0, f),
}

pub SubcItems: Vec<(String, Comp)> = {
    <name:ID> "=" <comp:CompInst> => vec![(name, comp)],
    <name:ID> "=" <comp:CompInst> "," <cs:SubcItems> => {
        cs.into_iter().chain(vec![(name, comp)].into_iter()).collect()
    },
}

pub SubcDecl: HashMap<String, Comp> = {
    "subc" "(" <cs:SubcItems> ")" => {
        let mut map = HashMap::new();
        for (name, comp) in cs {
            map.insert(name, comp);
        }
        map
    },
}

pub DesignDecl: Vec<ConnDecl> = {
    "design" "(" <cs:ConnItems> ")" => cs,
}

pub IDItems: Vec<String> = {
    <name:ID> => vec![name],
    <name:ID> "," <ns:IDItems> => {
        vec![name].into_iter().chain(ns.into_iter()).collect()
    },
}

pub InsDecl: Vec<PinDecl> = {
    "ins" "(" ")" => vec![],
    "ins" "(" <ins:PinDeclItems> ")" => ins,
}

pub OutsDecl: Vec<PinDecl> = {
    "outs" "(" ")" => vec![],
    "outs" "(" <outs:PinDeclItems> ")" => outs,
}

pub CompDecl: CompDecl = {
    <name:ID> "(" <subc:SubcDecl> <ins_decl:InsDecl> <outs_decl:OutsDecl> <design:DesignDecl> ")" => {
        let mut idx: usize = 0;
        let ins: HashMap<String, (usize, u8)> = ins_decl.into_iter().map(|p| {
            let entry = (p.name, (idx, p.len));
            idx += p.len as usize;
            entry
        }).collect();

        idx = 0;

        let outs: HashMap<String, (usize, u8)> = outs_decl.into_iter().map(|p| {
            let entry = (p.name, (idx, p.len));
            idx += p.len as usize;
            entry
        }).collect();
        CompDecl { name, subc, ins, outs, design }
    }
}

pub CompDeclItems: Vec<CompDecl> = {
    <c:CompDecl> => vec![c],
    <c:CompDecl> <cs:CompDeclItems> => {
        vec![c].into_iter().chain(cs.into_iter()).collect()
    },
}

pub ImportStatm: String = {
    "use" <path:Path> ";" => path,
}

pub ImportItems: Vec<String> = {
    <u:ImportStatm> => vec![u],
    <u:ImportStatm> <us:ImportItems> => {
        vec![u].into_iter().chain(us.into_iter()).collect()
    },
}

pub Circuit: Circuit = {
    <imports: ImportItems?> <c:CompDeclItems> => Circuit { imports, comps: c },
};